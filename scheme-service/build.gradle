plugins {
    id 'io.freefair.lombok' version '9.1.0'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
    id "io.sentry.jvm.gradle" version "5.12.2"
}

def versions = [
        sentry 						: '8.29.0'
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/FeeSchemeApplication.class', '**/config/**', '**/enums/**', '**/entity/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/FeeSchemeApplication.class', '**/config/**', '**/enums/**', '**/entity/**'])
        }))
    }

    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'LINE'
                minimum = 0.90
            }

            limit {
                counter = 'BRANCH'
                minimum = 0.80
            }
        }
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationtest/java')
        resources.srcDir file('src/integrationtest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.named('processIntegrationTestResources', Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
check.dependsOn jacocoTestCoverageVerification

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentry) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

dependencies {
    implementation project(':scheme-api')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.14'

    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.14'

    implementation 'org.apache.commons:commons-lang3:3.20.0'

    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'

    implementation 'org.postgresql:postgresql:42.7.8'

// https://mvnrepository.com/artifact/org.flywaydb/flyway-core
    implementation 'org.flywaydb:flyway-core:11.17.1'
    implementation 'org.flywaydb:flyway-database-postgresql:11.17.1'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-flyway
    implementation 'org.springframework.boot:spring-boot-flyway:4.0.0'

    implementation 'org.springframework.boot:spring-boot-starter-webmvc'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.16.0'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'org.springframework.boot:spring-boot-starter-webmvc-test'

    implementation 'org.springframework.boot:spring-boot-test-autoconfigure'

    testImplementation 'org.testcontainers:junit-jupiter:1.21.3'
    testImplementation 'org.testcontainers:postgresql:1.21.3'
    // CVE-2024-25710
    testImplementation 'org.apache.commons:commons-compress:1.28.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback:$versions.sentry"
    implementation "io.sentry:sentry-spring-boot-4:$versions.sentry"
}
