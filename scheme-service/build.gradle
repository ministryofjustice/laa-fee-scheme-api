plugins {
    id 'io.freefair.lombok' version '9.0.0'
    id 'uk.gov.laa.springboot.laa-spring-boot-gradle-plugin'
    id "io.sentry.jvm.gradle" version "5.12.1"
}

def versions = [
        sentry 						: '8.24.0'
]

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/FeeSchemeApplication.class', '**/config/**', '**/enums/**'])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['**/FeeSchemeApplication.class', '**/config/**', '**/enums/**'])
        }))
    }

    violationRules {
        rule {
            element = 'CLASS'

            limit {
                counter = 'LINE'
                minimum = 0.80
            }

            limit {
                counter = 'BRANCH'
                minimum = 0.70
            }
        }
    }
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationtest/java')
        resources.srcDir file('src/integrationtest/resources')
        compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

tasks.named('processIntegrationTestResources', Copy) {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

tasks.named('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
}

check.dependsOn integrationTest
check.dependsOn jacocoTestCoverageVerification

dependencyManagement {
    dependencies {
        dependencySet(group: 'io.sentry', version: versions.sentry) {
            entry 'sentry-spring-boot-starter'
            entry 'sentry-logback'
        }
    }
}

dependencies {
    implementation project(':scheme-api')

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.13'

    implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.13'

    implementation 'org.apache.commons:commons-lang3:3.19.0'

    implementation 'uk.gov.laa.springboot:laa-spring-boot-starter-auth'

    implementation 'org.postgresql:postgresql:42.7.8'

// https://mvnrepository.com/artifact/org.flywaydb/flyway-core
    implementation 'org.flywaydb:flyway-core:11.15.0'
    implementation 'org.flywaydb:flyway-database-postgresql:11.15.0'

    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.5'

    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    testImplementation 'org.testcontainers:junit-jupiter:1.21.3'
    testImplementation 'org.testcontainers:postgresql:1.21.3'
    // CVE-2024-25710
    testImplementation 'org.apache.commons:commons-compress:1.28.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation "io.sentry:sentry-logback:$versions.sentry"
    implementation "io.sentry:sentry-spring-boot-starter-jakarta:$versions.sentry"
}
